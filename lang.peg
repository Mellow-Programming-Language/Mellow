Program :: ( VariantDef
           | StructDef
           | FuncDef
           | ExternStructDecl
           | ExternFuncDecl
           | ImportStmt
           | InterfaceDef
           | UnittestBlock
           )+
           ;

Sp :: /\s+/;

ImportStmt :: #'import' #Sp ImportLit #";";
ImportLit :: /[a-zA-Z_][a-zA-Z0-9_]*(\.[a-zA-Z_][a-zA-Z0-9_]*)*/;

ExternStructDecl :: #'extern' #Sp #'struct' #Sp Identifier #";";
ExternFuncDecl   :: #'extern' #Sp #'func'   #Sp Identifier
                    FuncDefArgList FuncReturnType #";";

StructDef :: #'struct' #Sp Identifier TemplateTypeParams StructBody;
StructBody :: #"{" (StructEntry | StructFunction)+ #"}";
StructEntry :: VariableTypePair #";";
StructFunction :: FuncDef;

FuncDef :: FuncSignature FuncBodyBlocks;
FuncSignature :: #'func' #Sp Identifier TemplateTypeParams FuncDefArgList FuncReturnType;
FuncDefArgList :: #"(" (FuncSigArg (#"," FuncSigArg)*)? #")";
FuncSigArg :: Identifier #":" ^StorageClass* TypeId;
FuncReturnType :: (#":" (TypeId | TypeTuple))?;
FuncBodyBlocks :: ( BareBlock
                  | (InBlock? OutBlock? ReturnModBlock? BodyBlock)
                  );
InBlock :: #"in" BareBlock;
OutBlock :: #"out" BareBlock;
ReturnModBlock :: #'return' ((#Sp Identifier) | (#"" IdTuple))? BareBlock;
BodyBlock :: #"body" BareBlock;

BareBlock :: #"{" Statement* #"}";

UnittestBlock :: #"unittest" BareBlock;

StorageClass :: ConstClass;
ConstClass :: #'const' #Sp;

VariantDef :: #'variant' #Sp Identifier TemplateTypeParams VariantBody;
VariantBody ::  #"{" VariantEntry+ #"}";
VariantEntry :: Identifier (#"(" TypeId (#"," TypeId)* #")")? #","?;

InterfaceDef :: #'interface' #Sp Identifier TemplateTypeParams InterfaceBody;
InterfaceBody :: #"{" InterfaceEntry+ #"}";
InterfaceEntry :: FuncSignature #";";

Statement :: BareBlock
           | FuncDef
           | (ReturnStmt #";")
           | IfStmt
           | WhileStmt
           | ForStmt
           | ForeachStmt
           | MatchStmt
           | (Declaration #";")
           | (AssignExisting #";")
           | (AssertStmt #";")
           | (SpawnStmt #";")
           | (YieldStmt #";")
           | (BreakStmt #";")
           | (ContinueStmt #";")
           | (ChanWrite #";")
           | (FuncCall #";")
           ;

AssertStmt :: #"assert" #"(" BoolExpr (#"," BoolExpr)? #")";

ReturnStmt :: #'return' #Sp BoolExpr;

IfStmt :: #"if" #"(" CondAssignments (IsExpr | BoolExpr) #")"
          BareBlock ElseIfs EndBlocks?;
ElseIfs :: ElseIfStmt*;
ElseIfStmt :: #'else' #Sp #"if" #"(" CondAssignments (IsExpr | BoolExpr) #")"
              BareBlock;

WhileStmt :: #"while" #"(" CondAssignments (IsExpr | BoolExpr) #")" BareBlock
             EndBlocks?;

ForStmt :: #"for" #"(" CondAssignments BoolExpr? #";" ForUpdateStmt? #")"
           BareBlock EndBlocks?
           ;
ForUpdateStmt :: AssignExisting (#"," AssignExisting)*;

ForeachStmt :: #"foreach" #"(" CondAssignments ForeachArgs #";" BoolExpr #")"
               BareBlock EndBlocks?
               ;
ForeachArgs :: Identifier (#"," Identifier)*;

EndBlocks :: ThenElseCoda
           | ThenCodaElse
           | ElseThenCoda
           | ElseCodaThen
           | CodaElseThen
           | CodaThenElse
           | ThenElse
           | ThenCoda
           | ElseThen
           | ElseCoda
           | CodaThen
           | CodaElse
           | ThenBlock
           | ElseBlock
           | CodaBlock
           ;
ThenElseCoda :: ^ThenBlock ^ElseBlock ^CodaBlock;
ThenCodaElse :: ^ThenBlock ^CodaBlock ^ElseBlock;
ElseThenCoda :: ^ElseBlock ^ThenBlock ^CodaBlock;
ElseCodaThen :: ^ElseBlock ^CodaBlock ^ThenBlock;
CodaElseThen :: ^CodaBlock ^ElseBlock ^ThenBlock;
CodaThenElse :: ^CodaBlock ^ThenBlock ^ElseBlock;
ThenElse :: ^ThenBlock ^ElseBlock;
ThenCoda :: ^ThenBlock ^CodaBlock;
ElseThen :: ^ElseBlock ^ThenBlock;
ElseCoda :: ^ElseBlock ^CodaBlock;
CodaThen :: ^CodaBlock ^ThenBlock;
CodaElse :: ^CodaBlock ^ElseBlock;
ThenBlock :: #"then" BareBlock;
ElseBlock :: #"else" BareBlock;
CodaBlock :: #"coda" BareBlock;

Declaration :: DeclAssignment | DeclTypeInfer | VariableTypePair;

SpawnStmt :: #'spawn' #Sp Identifier TemplateInstantiation? FuncCallArgList;

YieldStmt :: #'yield' #Sp?;

BreakStmt :: #'break' #Sp?;

ContinueStmt :: #'continue' #Sp?;

ChanWrite :: BoolExpr #"<-=" BoolExpr;

FuncCall :: Identifier TemplateInstantiation? FuncCallArgList;




DeclAssignment :: (VariableTypePairTuple | VariableTypePair) #"=" BoolExpr;
DeclTypeInfer :: (IdTuple | Identifier) #":=" BoolExpr;
AssignExisting :: LorRValue ^AssignExistingOp BoolExpr;
AssignExistingOp :: "="
                  | "+="
                  | "-="
                  | "/="
                  | "*="
                  | "%="
                  | "~="
                  ;
LorRValue :: Identifier LorRTrailer?;
LorRTrailer :: (#"." Identifier LorRTrailer?)
             | (#"[" SingleIndex #"]" LorRTrailer?)
             ;
Assignment :: DeclAssignment | DeclTypeInfer | AssignExisting;
CondAssignments :: CondAssign*;
CondAssign :: (Assignment | VariableTypePair) #";";





IsExpr :: BoolExpr #'is' #Sp ^VariantIsMatch;
VariantIsMatch :: Identifier (#"(" ^IdOrWildcard (#"," ^IdOrWildcard)* #")")?;
IdOrWildcard :: WildcardPattern | Identifier;

BoolExpr :: OrTest;
OrTest :: AndTest (#"||" AndTest)*;
AndTest :: NotTest (#"&&" NotTest)*;
NotTest :: (#"!" NotTest) | Comparison;
Comparison :: Expr (^CompOp Expr)?;
CompOp :: "<="
        | ">="
        | ('<' #Sp)
        | ('>' #Sp)
        | "=="
        | "!="
        | "<in>"
        | ('in' #Sp)
        ;
Expr :: OrExpr;
OrExpr :: XorExpr (#"|" XorExpr)*;
XorExpr :: AndExpr (#"^" AndExpr)*;
AndExpr :: ShiftExpr (#"&" ShiftExpr)*;
ShiftExpr :: SumExpr (("<<" | ">>") SumExpr)*;
SumExpr :: ProductExpr (^SumOp ProductExpr)*;
SumOp :: "+"
       | "-"
       | "~"
       | "<|>"
       | "<&>"
       | "<^>"
       | "<->"
       ;
ProductExpr :: Value (("*" | "/" | "%") Value)*;
Value :: (BooleanLiteral DotAccess?)
       | Lambda
       | StructConstructor
       | (CharLit DotAccess?)
       | (StringLit DotAccess?)
       | ValueTuple
       | (ParenExpr DotAccess?)
       | (ArrayLiteral DotAccess?)
       | (Number DotAccess?)
       | (ChanRead DotAccess?)
       | (Identifier Trailer?)
       | SliceLengthSentinel
       ;

BooleanLiteral :: ("true")
                | ("false")
                ;

Lambda :: #"fn" LambdaArgs #"=>" BoolExpr;
LambdaArgs :: #"(" (VariableTypePair (#"," VariableTypePair)*)? #")";

StructConstructor :: Identifier TemplateInstantiation?
                                #"{" ^StructMemberConstructor
                                     (#"," ^StructMemberConstructor)*
                                #"}";
StructMemberConstructor :: Identifier #"=" BoolExpr;

ValueTuple :: #"(" BoolExpr (#"," BoolExpr)+ #")";

ParenExpr :: #"(" BoolExpr #")";

ArrayLiteral :: #"[" (Value (#"," Value)* #","?)? #"]";

Number :: FloatNum | IntNum;
CharLit :: /'(?:\\.|[^'\\])'/;
StringLit :: (/"(?:\\.|[^"\\])*"/ | /\x60(?:[^\x60]*)\x60/)+;
IntNum :: /-?[1-9][0-9]*|0/;
FloatNum :: /-?[0-9]+\.[0-9]+/;

SliceLengthSentinel :: #"$";

ChanRead :: #"<-" BoolExpr;

Trailer :: DynArrAccess
         | TemplateInstanceMaybeTrailer
         | FuncCallTrailer
         | DotAccess
         ;
DynArrAccess :: Slicing Trailer?;
TemplateInstanceMaybeTrailer :: TemplateInstantiation Trailer?;
FuncCallTrailer :: FuncCallArgList Trailer?;
Slicing :: #"["
                ( IndexRange
                | SingleIndex
                )
           #"]"
           ;
SingleIndex :: BoolExpr;
IndexRange :: StartToIndexRange
            | IndexToIndexRange
            | IndexToEndRange
            ;
StartToIndexRange :: #".." BoolExpr;
IndexToEndRange :: BoolExpr #"..";
IndexToIndexRange :: BoolExpr #".." BoolExpr;
FuncCallArgList :: #"(" (BoolExpr (#"," BoolExpr)*)? #")";
DotAccess :: #"." Identifier Trailer?;







MatchStmt :: #"match" #"(" CondAssignments BoolExpr #")" #"{" MatchWhen+ #"}"
             EndBlocks?
             ;
MatchWhen :: Pattern (#"if" #"(" CondAssignments BoolExpr #")")?
             #"::" Statement;
Pattern :: DestructVariantPattern
         | StructPattern
         | BoolPattern
         | StringPattern
         | CharPattern
         | FloatPattern
         | IntPattern
         | TuplePattern
         | ArrayEmptyPattern
         | ArrayPattern
         | ArrayTailPattern
         | WildcardPattern
         | VarOrBareVariantPattern
         ;
DestructVariantPattern  :: Identifier #"(" Pattern (#"," Pattern)* #")";
StructPattern           :: Identifier? #"{" Identifier #"=" Pattern
                          (#"," Identifier #"=" Pattern)* #"}";
BoolPattern             :: "true" | "false";
StringPattern           :: StringLit;
CharPattern             :: CharLit (#".." CharLit)?;
IntPattern              :: IntNum (#".." IntNum)?;
FloatPattern            :: FloatNum (#".." FloatNum)?;
TuplePattern            :: #"(" Pattern (#"," Pattern)+ #")";
ArrayEmptyPattern       :: #"[" #"]";
ArrayPattern            :: #"[" Pattern (#"," Pattern)*
                         ( #"," ".." (#'as' #Sp Identifier)? )? #"]";
ArrayTailPattern        :: #"[" #".." (#'as' #Sp Identifier)? #","
                           Pattern (#"," Pattern)* #"]";
WildcardPattern         :: #"_";
VarOrBareVariantPattern :: Identifier;

VariableTypePair :: Identifier #":" TypeId;
VariableTypePairTuple :: #"(" VariableTypePair (#"," VariableTypePair)* #")";

Identifier :: /([a-zA-Z]|_[a-zA-Z0-9])[a-zA-Z0-9_]*/;
IdTuple :: #"(" Identifier (#"," Identifier)* #")";

TypeId :: ChanType
        | BasicType
        | ArrayType
        | SetType
        | HashType
        | FuncPtrType
        | UserType
        ;
ChanType :: #"chan" #"!" ((#"(" TypeId #")") | (TypeId));
ArrayType :: #"[" BoolExpr? #"]" TypeId;
SetType :: #"<" #">" BasicType;
HashType :: #"[" BasicType #"]" TypeId;
TypeTuple :: #"(" TypeId (#"," TypeId)+ #")";
UserType :: Identifier TemplateInstantiation?;
FuncPtrType :: #"func" #"(" (TypeId (#"," TypeId)*)? #")" (":" TypeId)?;
BasicType :: "void"
           | "long"
           | "int"
           | "short"
           | "byte"
           | "float"
           | "double"
           | "char"
           | "bool"
           | "string"
           ;

TemplateInstantiation :: #"!" TemplateParam;
TemplateParam :: TemplateParamList | TypeId;
TemplateParamList :: #"(" TemplateAlias (#"," TemplateAlias)* #")";
TemplateAlias :: Lambda | TypeId;
TemplateTypeParams :: TemplateTypeParamList?;
TemplateTypeParamList :: #"(" Identifier (#"," Identifier)* #")";
